apply plugin: 'com.android.application'
apply plugin: 'kotlin-android'
apply plugin: 'kotlin-android-extensions'
apply plugin: 'kotlin-kapt'
apply plugin: 'com.chaquo.python'

def REPO_ROOT = "../.."
def GENERATED_DIR = "$buildDir/generated/ec"
mkdir(GENERATED_DIR)
def STRINGS_DIR = "$GENERATED_DIR/strings"
def REQUIREMENTS_TXT = "$GENERATED_DIR/requirements.txt"

android {
    compileOptions {
        // Required by ACRA.
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }
    compileSdkVersion 29

    defaultConfig {
        applicationId "org.electroncash.wallet"
        minSdkVersion 18  // Older versions give a "too many libraries" error.
        targetSdkVersion 29

        def ecVersion = null
        file("$REPO_ROOT/electroncash/version.py").eachLine {
            def matcher = it =~ / *PACKAGE_VERSION *= *'([0-9.]+)'.*/
            if (matcher.matches()) {
                ecVersion = matcher.group(1)
            }
        }
        if (ecVersion == null) {
            throw new GradleException("Couldn't find version number")
        }
        def BUILD_NUM = 3  // Distinguish multiple releases with the same version number.
        versionName "$ecVersion-$BUILD_NUM"
        def verParsed = ecVersion.split(/\./).collect { Integer.parseInt(it) }
        versionCode((verParsed[0] * 1000000) + (verParsed[1] * 10000) + (verParsed[2] * 100) +
                    BUILD_NUM)

        vectorDrawables.useSupportLibrary = true
        testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"

        python {
            pip {
                install "-r", REQUIREMENTS_TXT  // Generated by generateRequirementsTxt below.
                install "-r", "$REPO_ROOT/contrib/deterministic-build/requirements-android.txt"
            }
            pyc {
                src true
                pip true
            }
        }
        ndk {
            abiFilters "armeabi-v7a", "arm64-v8a", "x86", "x86_64"
        }
    }

    dataBinding.enabled = true

    sourceSets {
        main {
            python {
                srcDir REPO_ROOT
                include "electroncash/"
                exclude "electroncash/locale/"
                include "electroncash_gui/__init__.py"
                include "electroncash_plugins/__init__.py"

                // From src/main/python
                include "chaquopy/"
                include "electroncash_gui/android/"
            }
            res {
                srcDir STRINGS_DIR  // Generated by generateStrings below.
            }
        }
    }

    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
        }
    }

    flavorDimensions "version"
    productFlavors {
        MainNet {
            buildConfigField "boolean", "testnet", "false"
        }
        TestNet {
            buildConfigField "boolean", "testnet", "true"
        }
    }

    lintOptions {
        disable "MissingTranslation"
    }

    def keystore = file("../keystore.jks")
    if (keystore.exists()) {
        signingConfigs {
            config {
                storeFile keystore
                keyAlias "key0"
                keyPassword "android"
                storePassword "android"
            }
        }
        buildTypes.all { it.signingConfig signingConfigs.config }
    }

    applicationVariants.all { variant ->
        variant.outputs.all {
            outputFileName = "electroncash-${variant.versionName}.apk"
        }
    }
}

afterEvaluate {
    // See "Strings" section in README.md.
    task("generateStrings", type: Exec) {
        def scriptFilename = "generate_strings.py"
        inputs.file scriptFilename
        inputs.file "src/main/python/electroncash_gui/android/strings.py"
        outputs.upToDateWhen { file(STRINGS_DIR).exists() }
        doFirst {
            delete(STRINGS_DIR)
        }
        def pyVersion = "3"
        if (System.getProperty("os.name").startsWith("Windows")) {
            executable "py"
            args "-$pyVersion"
        } else {
            executable "python$pyVersion"
        }
        args scriptFilename, STRINGS_DIR
    }
    for (variant in android.applicationVariants) {
        tasks.getByName("generate${variant.name.capitalize()}Resources")
            .dependsOn("generateStrings")
    }

    task("deleteStrings") {
        doLast { delete(STRINGS_DIR) }
    }
    task("regenerateStrings") {
        dependsOn ("deleteStrings", "generateStrings")
    }

    // Remove unnecessary requirements (#2162).
    task("generateRequirementsTxt") {
        inputs.file "$REPO_ROOT/contrib/deterministic-build/requirements.txt"
        outputs.file REQUIREMENTS_TXT
        doLast {
            def buffer = new StringBuilder()
            file(REQUIREMENTS_TXT).withPrintWriter { output ->
                file(inputs.files.singleFile).eachLine { line ->
                    buffer.append(line + "\n")
                    if (!line.endsWith("\\")) {
                        if (!(buffer =~ /^(pip|QDarkStyle|setuptools|wheel)==/)) {
                            output.print(buffer)
                        }
                        buffer.setLength(0)
                    }
                }
            }
        }
    }
    for (variant in android.applicationVariants) {
        tasks.getByName("generate${variant.name.capitalize()}PythonRequirements")
            .dependsOn("generateRequirementsTxt")
    }
}

dependencies {
    implementation "org.jetbrains.kotlin:kotlin-stdlib-jdk7:$kotlinVersion"

    implementation 'androidx.appcompat:appcompat:1.1.0'
    implementation 'androidx.constraintlayout:constraintlayout:1.1.3'
    implementation 'androidx.fragment:fragment-ktx:1.1.0'
    implementation 'androidx.preference:preference:1.0.0'
    implementation 'androidx.lifecycle:lifecycle-extensions:2.1.0'
    implementation 'androidx.lifecycle:lifecycle-livedata-ktx:2.1.0'
    implementation 'com.google.android.material:material:1.0.0'

    // As recommended in zxing-android-embedded documentation, reduce zxing:core version
    // number, otherwise we require API level 19.
    implementation('com.journeyapps:zxing-android-embedded:3.6.0') { transitive = false }
    implementation 'com.google.zxing:core:3.3.0'

    def acraVersion = "5.2.0"
    implementation "ch.acra:acra-core:$acraVersion"
    implementation "ch.acra:acra-dialog:$acraVersion"
    implementation "ch.acra:acra-http:$acraVersion"

    testImplementation 'junit:junit:4.12'
    androidTestImplementation 'androidx.test:runner:1.1.0'
    androidTestImplementation 'androidx.test.espresso:espresso-core:3.1.0'
}
